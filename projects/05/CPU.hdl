// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                            * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input
 * holds the value of this location. If the current instruction needs
 * to write a value to M, the value is placed in outM, the address
 * of the target location is placed in the addressM output, and the
 * writeM control bit is asserted. (When writeM==0, any value may
 * appear in outM). The outM and writeM outputs are combinational:
 * they are affected instantaneously by the execution of the current
 * instruction. The addressM and pc outputs are clocked: although they
 * are affected by the execution of the current instruction, they commit
 * to their new values only in the next time step. If reset==1 then the
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather
 * than to the address resulting from executing the current instruction.
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    //如果sel是1，就是C指令，b=in，isC=1 isA=0，isC和isA总有一个对的，sel=0同理，总有一个是对的一个是错的。
    DMux(in=true,sel=instruction[15],a=isA,b=isC);


    //如果是C指令
    And(a=isC,b=instruction[12],out=isM);
    And(a=isC,b=instruction[11],out=zx);
    And(a=isC,b=instruction[10],out=nx);
    And(a=isC,b=instruction[9],out=zy);
    And(a=isC,b=instruction[8],out=ny);
    And(a=isC,b=instruction[7],out=f);
    And(a=isC,b=instruction[6],out=no);
    And(a=isC,b=instruction[5],out=destA);
    And(a=isC,b=instruction[4],out=destD);
    And(a=isC,b=instruction[3],out=writeM);
    And(a=isC,b=instruction[2],out=isJLT);
    And(a=isC,b=instruction[1],out=isJEQ);
    And(a=isC,b=instruction[0],out=isJGT);
    And(a=isJLT,b=ng,out=JLT);
    And(a=isJEQ,b=zr,out=JEQ);
    Not(in=ng,out=notNg);
    Not(in=zr,out=notZr);
    And(a=notNg,b=notZr,out=positive);
    And(a=isJGT,b=positive,out=JGT);
    Or(a=JLT,b=JEQ,out=jumpTemp);
    Or(a=jumpTemp,b=JGT,out=jump);


    //对于第一个选择器，是ALU的输出还是instruction作为A寄存器的输入 由控制单元决定，控制单元是由C指令解析出来的。
    Mux16(a=outALU,b=instruction,sel=isA,out=outFirstMux);

    //对于A寄存器，如果指令是A指令或则instruction[10]为1的C指令。
    Or(a=isA,b=destA,out=loadA);
    ARegister(in=outFirstMux,load=loadA,out=outA,out[0..14]=addressM);

    //对于A/M选择器:如果sel是0 out就是outA的值，如果sel=1，out就是outA表示的地址中的数据即inM。
    Mux16(a=outA,b=inM,sel=isM,out=outAM);

    //对于D寄存器，
    DRegister(in=outALU,load=destD,out=outD);

    //对于ALU

    ALU(x=outD,y=outAM,zx=zx,nx=nx,zy=zy,ny=ny,f=f,no=no,ng=ng,zr=zr,out=outM,out=outALU);

    //对于PC程序计数器
    PC(in=outA,inc=true,load=jump,reset=reset,out[0..14]=pc);
}
